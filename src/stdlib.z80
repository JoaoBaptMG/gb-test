;
; stdlib.z80
; Utilty functions
;

section "bank 0 stdlib.z80", rom0

;;
; Copies BC bytes from DE to HL.
MemoryCopy::
    inc b
    inc c
    jr .begin
.loop:
    ld a, [de]
    ld [hl+], a
    inc de
.begin:
    dec c
    jr nz, .loop
    dec b
    jr nz, .loop
    ret

;;
; Fills BC bytes in HL with A
MemoryFill::
    inc b
    inc c
    jr .begin
.loop:
    ld [hl+], a
.begin:
    dec c
    jr nz, .loop
    dec b
    jr nz, .loop
    ret

; Dynamic call from BC
DynamicBC::
    push bc
    ret

; Dynamic call from DE
DynamicDE::
    push de
    ret

; Dynamic call from HL
DynamicHL::
    push hl
    ret

;;
; Copy a full screen tilemap to VRAM
CopyFullScreenVRAM::
    ld c, 18
.copyNewRowToVRAM:
    ld b, 20
.copyTilemapToVRAM:
    ld a, [hl+]
    ld [de], a
    inc de
    dec b
    jr nz, .copyTilemapToVRAM
    ld a, e
    add a, 12
    ld e, a
    adc a, d
    sub e
    ld d, a
    dec c
    jr nz, .copyNewRowToVRAM
    ret

; Multiply A by DE and put in HL
HLeqAxDE::
    ld hl, 0
    rla
    jr nc, .noAdd
    add hl, de
.noAdd
    rept 7
    add hl, hl
    rla
    jr nc, .endMulLoop\@
    add hl, de
.endMulLoop\@
    endr
    ret

; Multiply A by DE and put in HL
HLeqAxBC::
    ld hl, 0
    rla
    jr nc, .noAdd
    add hl, bc
.noAdd
    rept 7
    add hl, hl
    rla
    jr nc, .endMulLoopx\@
    add hl, bc
.endMulLoopx\@
    endr
    ret
